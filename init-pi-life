#!/usr/bin/env perl
use strict;
use warnings;

# Arrange the binary bits of pi in a square grid, and use as the initial GoL state.
# A stable state is reached at the 400th iteration for a 48x48 surface.
# A stable state is reached at the 748th iteration for a 96x96 surface.

# After running this and generating the GoL frames. Use ImageMagick to increase size:
# $ cp init-pi-life-0001.png init-pi-life-0000.png
# $ convert 'init-pi-life-%04d.png[0-750]' -resize 192x192 'resized/resized%04d.png'
# $ rm resized/resized0000.png
# Then mash them into an animated gif:
# $ convert -delay 60 resized/resized*.png animated-pi-life.gif

use File::Slurper qw(read_text write_text);
use Game::Life;
use Imager;
use Imager::Color;
use Math::BigFloat;

my $iteration = shift || 1000;    # How many game frames?
my $row_size  = shift || 48 * 3;  # Size of the image in pixels

my $path = 'life'; # Subdirectory to house the generated frames

my $max = $row_size * $row_size / 3;  # Number of pi decimal digits

my $decimal; # The decimal digits

my $pi_digits_file = 'pi-digits-' . $max . '.txt'; # The file to hold our computed digits

print "Gathering $max decimals of pi...\n";
# Use the file of digits if it exists
if (-e $pi_digits_file) {
    $decimal = read_text($pi_digits_file);
    chomp $decimal;
}
else { # Get the digits ... very slow
    ($decimal = Math::BigFloat->bpi($max)) =~ s/^3\.(.+)$/$1/;
    write_text($pi_digits_file, $decimal);
}

my $init = []; # The initial GoL state

# Counters that keep track of the grid position
my $i = 0;
my $j = 0;

print "Constructing a $row_size x $row_size initial grid...\n";
for my $n (split //, $decimal) {
    my $binary = sprintf '%04b', $n; # Turn the decimal digit into a binary number

    # Add each bit to the grid
    for my $bit (split //, $binary) {
        push $init->[$i]->@*, $bit;
    }

    # Increment the counters
    if ($i > $row_size) {
        $i = 0;
        $j++;
    }
    else {
        $i++;
    }
}

my $img = Imager->new(xsize => $row_size, ysize => $row_size);

my $white = Imager::Color->new(255, 255, 255);
my $black = Imager::Color->new(  0,   0,   0);

my $game = Game::Life->new($row_size);
$game->place_points(0, 0, $init);

print "Rendering game of life frames...\n";
for my $n (1 .. $iteration) {
    $j = 0;

    my $grid = $game->get_grid;

    for my $row (@$grid) {
        $i = 0;

        for my $bit (@$row) {
            if ($bit) {
                $img->setpixel(x => $i, y => $j, color => $black);
            }
            else {
                $img->setpixel(x => $i, y => $j, color => $white);
            }

            $i++;
        }

        $j++;
    }

    $img->write(file => sprintf '%s/%s-%04d.png', $path, $0, $n) or die $img->errstr;

    $game->process();
}
