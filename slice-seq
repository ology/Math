#!/usr/bin/env perl
use strict;
use warnings;

use Math::Big qw(fibonacci primes);
use Math::BigInt try => 'GMP';

my $min = shift || 2;   # Minimum prime number
my $max = shift || 20;  # Maximum prime number
my $inc = shift || 10;  # Increment for prime numbers
my $start = shift || 2; # Minimum modulo
my $mod = shift || 6;   # Maximum modulo

die "Starting modulo must be greater than or equal to the maximum modulo\n"
    if $start > $mod;

my $w = length $max; # Field width for sprintf
my $z = length $mod; # "

# Create an HTML table for the images
my $html = "$0.html";
open(my $content, '>', $html) or die "Can't write to '$html': $!";
print $content "<html>\n<body>\n<h3>Prime limit to $max by $inc x Modulo to $mod</h3>\n<table>\n\n";

# Consider each modulo...
for my $m ($start .. $mod) {
    # Get the modulus as a bigint
    my $bm = Math::BigInt->new($m);

    print $content "<tr>\n\n"; # Start table row

    # Consider each prime limit
    for (my $x = $min; $x <= $max; $x += $inc) {
        # Make a file for the data
        my $name = sprintf '%s-%0*d-%0*d', $0, $w, $x, $z, $m;
        my $file = $name . '.dat';
        open(my $dat, '>', $file) or die "Can't write to '$file': $!";

        print $content "<td>"; # Start table cell

        my $i = 1; # Increment for the prime

        # Do the actual number crunching
        for my $n (primes($x)) {
            my $p = Math::BigInt->new($n);
            $p->bmod($bm);
            my $fib = fibonacci($n);
            my $f = Math::BigInt->new($fib);
            $f->bmod($bm);

            # Show our progress
            print "[$i,$x] mod=$m\tprime=$n ($p)\tfib=$fib ($f)\n";

            # Print a line to the data file
            print $dat "$p\t$f\n";

            $i++; # Increment the seen prime
        }

        print $content qq|<a href="$name.png"><img src="$name.png" width="100 height="100"/></a>|; # Table cell data

        print $content "</td>\n\n"; # End table cell

        close $dat;
    }

    print $content "</tr>\n\n"; # End table row
}

print $content "\n\n</table>\n</body>\n</html>";
close $content;
