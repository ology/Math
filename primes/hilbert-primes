#!/usr/bin/env perl

# Plot the primes on a Hilbert "curve."

use strict;
use warnings;

use Imager;
use Math::Curve::Hilbert;
use Math::Prime::XS qw( is_prime );

my $size        = shift || 1050;
my $max         = shift || 65521;
my $prime_color = shift || 'blue';
my $line_color  = shift || 'lightgray';

my $image = Imager->new( xsize => $size, ysize => $size );

$image->box( filled => 1, color => 'white' );

my $hilbert = Math::Curve::Hilbert->new(
    direction => 'up',
    max       => 8,
    clockwise => 1,
    step      => 4,
);

my $count = 0;

# Start the Hilbert curve
my ( $x1, $y1 ) = $hilbert->CoordinatesFromPoint( $count++ );

# Draw the Hilbert curve
while ( ( $hilbert->CoordinatesFromPoint($count) )[0] ) {
    # Set the line segment color
    my $color = $line_color;

    if ( $count > 1 && is_prime($count) ) {
        $color = $prime_color;
    }

    # Get a new point on the curve
    my ( $x2, $y2 ) = $hilbert->CoordinatesFromPoint( $count++ );

    # Draw a line segment
    $image->line(
        color => $color,
        x1 => $x1, x2 => $x2, y1 => $y1, y2 => $y2,
        aa => 1, endp => 1
    );

    # Increment the line segment
    ( $x1, $y1 ) = ( $x2, $y2 );

    # End the loop if we have reached the maximum
    last if $count > $max;
}

my $file = "$0.png";
$image->write( file => $file )
    or die "Can't save $file: ", $image->errstr;

warn "$file below $count\n";
