#!/usr/bin/env perl

# Plot the primes on a Hilbert "curve."

use strict;
use warnings;

use Imager;
use Math::Curve::Hilbert;
use Math::Prime::XS qw( is_prime );

# Set the image dimensions.
my $size        = shift || 1050;
my $max         = shift || 65521;
my $prime_color = shift || 'black';
my $line_color  = shift || 'lightgray';

# Instantiate an image.
my $image = Imager->new( xsize => $size, ysize => $size );

# Draw a filled box filling the image.
$image->box( filled => 1, color => 'white' );

# Instantiate a Hilbert curve.
my $hilbert = Math::Curve::Hilbert->new(
    direction => 'up',
    max       => 8,
    clockwise => 1,
    step      => 4,
);

# Declare an increment.
my $count = 0;

# Start the Hilbert curve.
my ( $x1, $y1 ) = $hilbert->CoordinatesFromPoint( $count++ );

# Draw the Hilbert curve.
while ( ( $hilbert->CoordinatesFromPoint($count) )[0] ) {
    # Set the line segment color.
    my $color = $line_color;

    if ( $count > 1 && is_prime($count) ) {
        # Output the increment
#        warn "$count\n";
        $color = $prime_color;
    }

    # Increment the Hilbert curve.
    my ( $x2, $y2 ) = $hilbert->CoordinatesFromPoint( $count++ );

    # Draw a line segment of the Hilbert curve onto the image.
    $image->line(
        color => $color,
        x1 => $x1, x2 => $x2, y1 => $y1, y2 => $y2,
        aa => 1, endp => 1
    );

    # Increment the line segment.
    ( $x1, $y1 ) = ( $x2, $y2 );

    # End the loop if we have reached the maximum count.
    last if $count > $max;
}

# Output the image as a file.
my $file = "$0.png";
$image->write( file => $file )
    or die "Can't save $file: ", $image->errstr;

# Output the final count.
warn "$file below $count\n";
