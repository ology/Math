#!/usr/bin/env perl
use strict;
use warnings;

use Chart::Plotly;
use Chart::Plotly::Plot;
use Chart::Plotly::Trace::Scatter;
use Chart::Plotly::Image::Orca;
use Math::Big 'primes';

my $mod  = shift || 6;
my $path = shift || '/Users/gene/tmp/primes-vs-next-prime';

my $size = 500;

die "Path $path does not exist" unless -d $path;

for my $max (qw( 10 100 1000 10000 )) {
    for my $modulo ( 2 .. $mod ) {
        my @xs;
        my @ys;
        my $previous;

        for my $p ( primes($max) ) {
            my $y = $p % $modulo;
            warn "$p % $modulo = $y\n";

            if ( $previous ) {
                my $x = $previous % $modulo;
                warn "\t$previous % $modulo = $x\n";

                push @xs, $x->numify();
                push @ys, $y->numify();
            }

            $previous = $p;
        }

        my $scatter = Chart::Plotly::Trace::Scatter->new( x => \@xs, y => \@ys );
        my $plot = Chart::Plotly::Plot->new();
        $plot->add_trace($scatter);
        $plot->layout({
            height => $size,
            width  => $size,
            title  => 'Primes below '. $max . ' Modulo ' . $modulo . ' vs Next Prime Modulo ' . $modulo,
            xaxis  => { title => 'Primes Modulo ' . $modulo, },
            yaxis  => { title => 'Next Prime Modulo ' . $modulo, },
        });
         
        Chart::Plotly::Image::Orca::orca(
            plot => $plot,
            file => "$path/primes-$max-mod-$modulo.png"
        );
    }
}
